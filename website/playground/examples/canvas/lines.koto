import color
from number import lerp
from play import random_color

line_color = color 'white'
background_color = color 'grey'
background_color.set_alpha 0.5
speed = 0.8
steps_min = 3
steps_max = 10
line_width = 5

export
  setup: ||
    play.show_canvas()

  update: |_, time|
    canvas.clear background_color
    w, h = canvas.width(), canvas.height()

    # Oscillate over time between 0 and 1
    z = (time * speed).sin() / 2 + 0.5

    steps = lerp steps_min, steps_max, z
    for i in 0..=steps
      step = i / steps
      x = w * step
      y = h * step
      z = line_width

      canvas.begin_path()
        .move_to x - z, -z
        .line_to w + z, h - y + z
        .move_to -z, y - z
        .line_to w - x + z, h + z
        .set_stroke_color line_color * (1.0 - step * 0.3)
        .set_line_width line_width
        .stroke()
