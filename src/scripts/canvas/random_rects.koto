# Draw random rectangles on the canvas

lerp = |min, max, x| min + (max - min) * x
random_float = |min, max| lerp min, max, random.number()
random_int = |min, max| random_float(min, max).round()

random_color = ||
  r = random_int 0, 255
  g = random_int 0, 255
  b = random_int 0, 255
  a = random_float 0.4, 0.6
  make_num4 r, g, b, a

play.setup = ||
  play.state =
    time: 0
    next_rect_time: 0

play.update = |time_delta|
  state = play.state
  state.time += time_delta

  if state.time > state.next_rect_time
    side_min = 20
    side_max = 400
    margin = side_min / 2

    x = random_int -margin, canvas.width() - margin
    y = random_int -margin, canvas.height() - margin
    width = random_int side_min, side_max
    height = random_int side_min, side_max

    canvas.set_fill_color random_color()
    canvas.fill_rect x, y, width, height

    state.next_rect_time = state.time + 0.1
