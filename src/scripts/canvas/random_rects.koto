# Draw random rectangles on the canvas

random_float = |min, max| min.lerp max, random.number()
random_int = |min, max| random_float(min, max).round()

random_color = ||
  r = random_int 0, 255
  g = random_int 0, 255
  b = random_int 0, 255
  a = random_float 0.4, 0.6
  make_num4 r, g, b, a

play.setup = ||
  canvas.set_fill_color random_color()
  canvas.fill_rect 0, 0, canvas.width(), canvas.height()
  return
    update_time: 0

play.update = |state, time|
  if time >= state.update_time
    side_min = 20
    side_max = 400
    margin = side_min / 2

    x = random_int -margin, canvas.width() - margin
    y = random_int -margin, canvas.height() - margin
    width = random_int side_min, side_max
    height = random_int side_min, side_max

    canvas.set_fill_color random_color()
    canvas.fill_rect x, y, width, height

    state.update_time = time + 0.1
